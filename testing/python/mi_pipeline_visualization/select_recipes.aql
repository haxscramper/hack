LET startNode = FIRST(
  FOR doc IN vertices
    FILTER doc.data.id == "modern_industrialization:crude_oil"
    RETURN doc
)

LET excludeNodes = UNIQUE(FLATTEN(
  FOR excludeId IN [
    "modern_industrialization:sulfuric_acid",
    "modern_industrialization:oxygen",
    "modern_industrialization:lubricant",
    "modern_industrialization:benzene",
    "minecraft:water"
  ]
    LET excludeNode = FIRST(
      FOR doc IN vertices
        FILTER doc.data.id == excludeId
        RETURN doc
    )
    FOR v IN 1..1 OUTBOUND excludeNode GRAPH "mi_recipes"
      RETURN v._id
))

LET traversal = (
  FOR v, e, p IN 1..8 OUTBOUND startNode GRAPH "mi_recipes"
    PRUNE v._id IN excludeNodes
    FILTER v._id NOT IN excludeNodes
    RETURN { v, e }
)

LET edgesOut = UNIQUE(traversal[*].e)

LET vertexIds = UNIQUE(FLATTEN(
  FOR e IN edgesOut
    RETURN [e._from, e._to]
))

LET verticesOut = DOCUMENT(vertices, vertexIds)

RETURN {
  vertices: verticesOut,
  edges: edgesOut
}
